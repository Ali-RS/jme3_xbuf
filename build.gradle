buildscript {
	repositories {
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "org.xtext:xtext-gradle-plugin:1.0.+"
		//classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
	}
}

//------------------------------------------------------------------------------
// Global
// content of this section could be placed in an other script
//------------------------------------------------------------------------------

allprojects {
	//apply plugin: 'nebula.lint'
	//gradleLint.rules = ['all-dependency']

	project.version = "git describe --always --dirty".execute().text.trim()
	println("version : '${project.version}'")
	group = "org.xbuf.jme3_xbuf"
	def description = 'xbuf lib and tools for jMonkeyEngine (jme3)'
}

subprojects {
	apply plugin: 'java' // assets are managed like java resource + code
	apply plugin: 'maven'
	apply plugin: 'eclipse'

	repositories {
		mavenLocal()
		jcenter()
		maven { url "http://dl.bintray.com/davidb/jme3stuff" }
		maven { url "https://jitpack.io" }
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	task writeNewPom << {
		pom {
			project {
				inceptionYear '2015'
				description project.description
				licenses {
					license {
						name "Public domain (CC0-1.0)"
						url "http://creativecommons.org/publicdomain/zero/1.0/"
						distribution "repo"
					}
				}
				developers {
					developer {
						id "davidB"
						name "David Bernard"
					}
					developer {
						id "riccardobl"
						name "Riccardo Balbo"
					}
				}
			}
		}.writeTo("$buildDir/newpom.xml")
	}
}
