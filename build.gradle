buildscript {
	repositories {
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
		classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.+'
		classpath "org.xtend:xtend-gradle-plugin:0.3.4"
		classpath "pl.allegro.tech.build:axion-release-plugin:1.3.0"
	}
}
apply plugin: "pl.allegro.tech.build.axion-release"
scmVersion {
		tag {
				prefix = ''
				versionSeparator = ''
		}
}

//------------------------------------------------------------------------------
// Global
// content of this section could be placed in an other script
//------------------------------------------------------------------------------

allprojects {
	group = "org.xbuf.jme3_xbuf"
	project.version = scmVersion.version
	def description = 'xbuf lib and tools for jMonkeyEngine (jme3)'
}

subprojects {
	apply plugin: 'java' // assets are managed like java resource + code
	apply plugin: 'maven'

	repositories {
		mavenLocal()
		jcenter()
		maven { url "http://updates.jmonkeyengine.org/maven/"}
		maven { url "http://dl.bintray.com/davidb/jme3stuff" }
		maven { url "https://jitpack.io" }
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	task writeNewPom << {
	    pom {
	        project {
            inceptionYear '2015'
						description project.description
						licenses {
							license {
								name "Public domain (CC0-1.0)"
								url "http://creativecommons.org/publicdomain/zero/1.0/"
								distribution "repo"
							}
						}
						developers {
							developer {
								id "davidB"
								name "David Bernard"
							}
						}
        }
	    }.writeTo("$buildDir/newpom.xml")
	}
}
